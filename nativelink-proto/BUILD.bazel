load("@rules_rust//rust:defs.bzl", "rust_binary", "rust_library")

PROTO_NAMES = [
    "com.github.bazelbuild.bazel.blaze",
    "com.github.bazelbuild.bazel.build_event_stream",
    "com.github.bazelbuild.bazel.blaze.invocation_policy",
    "com.github.bazelbuild.bazel.failure_details",
    "com.github.bazelbuild.bazel.options",
    "com.github.bazelbuild.bazel.command_line",
    "devtools.build.lib.packages.metrics",
    "build.bazel.remote.execution.v2",
    "build.bazel.semver",
    "com.github.trace_machina.nativelink.remote_execution",
    "google.api",
    "google.bytestream",
    "google.longrunning",
    "google.rpc",
]

rust_binary(
    name = "gen_protos_tool",
    srcs = ["gen_protos_tool.rs"],
    deps = [
        "@crates//:clap",
        "@crates//:prost-build",
        "@crates//:tonic-build",
    ],
)

genrule(
    name = "gen_rs_protos",
    srcs = [
        "com/github/bazelbuild/bazel/src/main/java/com/google/devtools/build/lib/buildeventstream/proto/build_event_stream.proto",
        "com/github/bazelbuild/bazel/src/main/java/com/google/devtools/build/lib/packages/metrics/package_load_metrics.proto",
        "com/github/bazelbuild/bazel/src/main/protobuf/command_line.proto",
        "com/github/bazelbuild/bazel/src/main/protobuf/failure_details.proto",
        "com/github/bazelbuild/bazel/src/main/protobuf/action_cache.proto",
        "com/github/bazelbuild/bazel/src/main/protobuf/option_filters.proto",
        "com/github/bazelbuild/bazel/src/main/protobuf/invocation_policy.proto",
        "build/bazel/remote/execution/v2/remote_execution.proto",
        "build/bazel/semver/semver.proto",
        "com/github/trace_machina/nativelink/remote_execution/worker_api.proto",
        "google/api/annotations.proto",
        "google/api/client.proto",
        "google/api/http.proto",
        "google/bytestream/bytestream.proto",
        "google/longrunning/operations.proto",
        "google/protobuf/any.proto",
        "google/protobuf/descriptor.proto",
        "google/protobuf/duration.proto",
        "google/protobuf/empty.proto",
        "google/protobuf/timestamp.proto",
        "google/protobuf/wrappers.proto",
        "google/rpc/status.proto",
    ],
    outs = ["{}.pb.rs".format(name) for name in PROTO_NAMES],
    cmd = '''
        set -e
        export PROTOC=$(execpath @protobuf//:protoc)

        $(execpath :gen_protos_tool) $(SRCS) -o $(RULEDIR)

        for file in $(RULEDIR)/*.rs; do
            mv -- "$$file" "$${file%.rs}.pb.rs"
        done
        ''',
    tools = [
        ":gen_protos_tool",
        "@protobuf//:protoc",
    ],
)

py_binary(
    name = "gen_lib_rs_tool",
    srcs = ["gen_lib_rs_tool.py"],
)

genrule(
    name = "gen_lib_rs",
    srcs = [":gen_rs_protos"],
    outs = ["lib.rs"],
    cmd = "$(execpath :gen_lib_rs_tool) --rootdir $(RULEDIR) $(SRCS) > $@",
    tools = [":gen_lib_rs_tool"],
)

rust_library(
    name = "nativelink-proto",
    srcs = glob(["genproto/*.rs"]),
    tags = ["no-rustfmt"],
    visibility = ["//visibility:public"],
    deps = [
        "@crates//:prost",
        "@crates//:prost-types",
        "@crates//:tonic",
    ],
)

py_binary(
    name = "update_protos",
    srcs = ["update_protos.py"],
    args = ["--update"] + PROTO_NAMES,
    data = [
        ":gen_lib_rs",
        ":gen_rs_protos",
    ],
)

# Test to ensure the proto files are in sync with the checked in files.
py_test(
    name = "update_protos_test",
    timeout = "short",
    srcs = ["update_protos.py"],
    args = ["--check"] + PROTO_NAMES,
    data = glob(["genproto/*.rs"]) + [
        ":gen_lib_rs",
        ":gen_rs_protos",
    ],
    main = "update_protos.py",
)
